# Maintainer: Hunter Wittenborn: <hunter@hunterwittenborn.com>

pkgname=makedeb
pkgver=2.12.7
pkgrel=1
pkgdesc="Create Debian archives from PKGBUILDs (git release)"
arch=('any')
license=('GPL3')
depends=('bash' 'binutils' 'file' 'dpkg-dev' 'makepkg')
optdepends=('git')

# Used to obtain folder names for local repository
_gitdir=$(git rev-parse --show-toplevel)
_foldername=$(basename "${_gitdir}")

source=("git+file://${_gitdir}")
sha256sums=('SKIP')

package() {
    # Create single file for makedeb
    mkdir -p "${pkgdir}/usr/bin"
    cd "${srcdir}/${_foldername}"

    # Add bash shebang
    echo '#!/bin/bash' > "${pkgdir}/usr/bin/makedeb"

    # Copy functions
    for i in $(find "src/functions/"); do
        if ! [[ -d "${i}" ]]; then
            cat "${i}" >> "${pkgdir}/usr/bin/makedeb"
        fi
    done
    cat "src/makedeb.sh" >> "${pkgdir}/usr/bin/makedeb"

    chmod 555 "${pkgdir}/usr/bin/makedeb"

    # Set package version and build type
    sed -i "s|package_version=.*|package_version=${pkgver}|" "${pkgdir}/usr/bin/makedeb"

    # Remove testing commands
    sed -i 's|.*# REMOVE AT PACKAGING||g' "${pkgdir}/usr/bin/makedeb"
}
